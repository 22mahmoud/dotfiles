#!/bin/sh

prompt_git_info() {
  [ -n "$vcs_info_msg_0_" ] \
    && echo " on %F{magenta} $vcs_info_msg_0_%f"
}

prompt_git_branch() {
  autoload -Uz vcs_info

  precmd_vcs_info() {
    vcs_info
  }

  precmd_functions+=(precmd_vcs_info)

  zstyle ':vcs_info:git*' check-for-changes true

  zstyle ':vcs_info:git:*' unstagedstr ' %F{green}!%f'
  zstyle ':vcs_info:git:*' stagedstr ' %F{yellow}+%f'

  zstyle ':vcs_info:git:*' formats '%b%u%c%m'
  zstyle ':vcs_info:git:*' actionformats '%b|%a%u%c%m'

  zstyle ':vcs_info:*' formats "%u%c%m"
  zstyle ':vcs_info:git*+set-message:*' hooks git-untracked

}

+vi-git-untracked() {
  if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
     git status --porcelain | grep -m 1 '^??' &>/dev/null
  then
    hook_com[misc]=' %F{red}?'
  fi
}

prompt_setup() {
  setopt prompt_subst

  prompt_git_branch

  typeset -g prompt_newline=$'\n%{\r%}'

  PROMPT=""
  PROMPT+='${prompt_newline}'
  PROMPT+='%F{blue}%B%1~%b%f'
  PROMPT+='$(prompt_git_info)'
  PROMPT+='${prompt_newline}%(?.%F{green}.%F{red})%B%b%f '
}

prompt_setup

# vim: ft=zsh
